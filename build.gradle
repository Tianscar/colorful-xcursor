plugins {
    id 'java-library'
    id 'signing'
    id 'maven-publish'
}

ext {
    libName = 'Colorful Xcursor'
    libGroupName = 'com.tianscar.awt.x11'
    libVendorName = 'Tianscar'

    libVersionCode = 5
    libVersionName = '1.1.2'

    libSourceCompatibility = JavaVersion.VERSION_1_8
    libTargetCompatibility = JavaVersion.VERSION_1_8
}

[compileJava, compileTestJava]*.options*.encoding = 'UTF-8'
sourceCompatibility = libSourceCompatibility
targetCompatibility = libTargetCompatibility

group = libGroupName
version = libVersionName

repositories {
    mavenCentral()
}

dependencies {
    implementation 'com.github.jnr:jnr-ffi:2.2.13'

    testImplementation 'org.junit.jupiter:junit-jupiter-api:5.8.1'
    testRuntimeOnly 'org.junit.jupiter:junit-jupiter-engine:5.8.1'
}

test {
    useJUnitPlatform()
}

java {
    withSourcesJar()
    withJavadocJar()
}

publishing {
    publications {
        mavenJava(MavenPublication) {
            groupId = libGroupName
            artifactId = rootProject.name
            version = libVersionName
            from components.java
            pom {
                name = libName
                description = 'Colorful custom cursor support for Java XAWT (AWT/X11)'
                url = 'https://github.com/Tianscar/colorful-xcursor'
                licenses {
                    license {
                        name = 'The MIT License'
                        url = 'https://spdx.org/licenses/MIT.html'
                    }
                }
                developers {
                    developer {
                        id = 'Tianscar'
                        name = 'Karstian Lee'
                        email = 'tianscar@protonmail.com'
                    }
                }
                scm {
                    connection = 'scm:git:git@github.com/Tianscar/colorful-xcursor.git'
                    developerConnection = 'scm:git:git@github.com/Tianscar/colorful-xcursor.git'
                    url = 'https://github.com/Tianscar/colorful-xcursor'
                }
            }
        }
    }
    repositories {
        maven {
            name = "OSSRH"
            if (project.version.toString().endsWith("-SNAPSHOT")) {
                url = "https://s01.oss.sonatype.org/content/repositories/snapshots"
            } else {
                url = "https://s01.oss.sonatype.org/service/local/staging/deploy/maven2/"
            }
            credentials {
                username = findProperty("ossrhUsername") ?: System.getenv("OSSRH_USERNAME")
                password = findProperty("ossrhPassword") ?: System.getenv("OSSRH_PASSWORD")
            }
        }
    }
}

signing {
    sign(publishing.publications.mavenJava)
}

javadoc {
    if(JavaVersion.current().isJava9Compatible()) {
        options.addBooleanOption('html5', true)
    }
}
